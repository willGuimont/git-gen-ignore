#!/usr/bin/env stack
-- stack --resolver lts-14.20 --install-ghc runghc --package http-conduit --package bytestring --package split --package directory --package monad-loops --package containers
{-# LANGUAGE OverloadedStrings #-}

import Control.Monad (unless, when)
import Control.Monad.Loops (whileM_)
import qualified Data.ByteString.Lazy.Char8 as L8
import Data.Char (toLower)
import Data.IORef (newIORef, readIORef, writeIORef)
import Data.List (intercalate, isInfixOf)
import Data.List.Split.Internals (splitOneOf)
import qualified Data.Set as Set
import Network.HTTP.Simple
import System.Directory (doesFileExist)
import System.Environment (getArgs)
import System.Exit (exitFailure, exitSuccess)

-- Networking
get :: String -> IO String
get url = do
  req <- parseRequest url
  response <- httpLBS req
  pure . L8.unpack . getResponseBody $ response

-- gitignore.io
baseUrl :: String
baseUrl = "https://www.toptal.com/developers/gitignore/api/"

formatUrl :: [String] -> String
formatUrl xs = baseUrl ++ params
  where
    params = intercalate "," $ map (map toLower) xs

getPossibleArgs :: IO [String]
getPossibleArgs = do
  content <- get $ baseUrl ++ "list"
  return $ splitOneOf ",\n" content

getErrors :: [String] -> IO [String]
getErrors xs = do
  possibleArgs <- getPossibleArgs
  let errors = filter (`notElem` possibleArgs) xs
  return errors

-- App
gitignoreFileName :: String
gitignoreFileName = ".gitignore"

getFromSearch :: [String] -> String -> [String]
getFromSearch possibles search = filter (isInfixOf search) possibles

data Answer
  = Yes
  | No
  | Unknown
  deriving (Eq)

main :: IO ()
main = do
  args <- getArgs
  when (null args) $ do
    putStrLn "Usage: "
    putStrLn "git genignore [list of OS, IDE, Programming languages separated by a space]"
    putStrLn "or"
    putStrLn "git genignore list [list of search terms separated by a space]"
    exitFailure
  when (head args == "list") $ do
    possibleArgs <- getPossibleArgs
    let searched = mconcat $ map (getFromSearch possibleArgs) $ tail args
    let unique = (Set.toList . Set.fromList) searched
    putStrLn $ intercalate "\n" unique
    exitSuccess
  errors <- getErrors args
  unless (null errors) $ do
    putStrLn $ "Error: some arguments were not recognized\n" ++ intercalate ", " errors
    exitFailure
  let formatedUrl = formatUrl args
  putStrLn $ "Fetching gitignore from " ++ formatedUrl
  content <- get formatedUrl
  fileAlreadyPresent <- doesFileExist gitignoreFileName
  when fileAlreadyPresent $ do
    userInput <- newIORef Unknown
    whileM_ ((Unknown ==) <$> readIORef userInput) $ do
      putStrLn $ gitignoreFileName ++ " is already present, overwrite it? [y/n]"
      input <- getLine
      let lower = map toLower input
      when (lower == "y") $ writeIORef userInput Yes
      when (lower == "n") $ writeIORef userInput No
    overwrite <- readIORef userInput
    case overwrite of
      Yes -> return ()
      No -> do
        putStrLn content
        exitSuccess
      _ -> exitFailure
  putStrLn $ "Writing into .gitignore"
  writeFile gitignoreFileName content
